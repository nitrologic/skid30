;
; imagine to skidmarks car converter
;

; needs buttons for origin adjustment
; needs docs
; needs wbstartup!
; needs header file
; header: Name | Designer | Settings | BitMap

WBStartup

i$="cars.imp/"
p$="ram:"
v$="Caravan"
s$="Simon"

fof=385

NEWTYPE .handling
  speed.w:accel.w:slide.w:steer.w
  bounce.w:penalty.w:shadowx.w:shadowy.w
  pad.w[8]
End NEWTYPE

NEWTYPE .header
  name$
  designer$
  settings.handling
  pic.b[64*32/4]
End NEWTYPE

DEFTYPE .header car

Function.w pickcolor{ch.l,x.w,y.w,settings.l,shif.w}
  MOVE.l d0,a0:MOVE 22(a0),d0:MOVE.l (a0),a0:MOVE.l d3,a1
  ADD d1,a0:MOVE d2,d3:MULU d0,d3:ADD.ld3,a0:ADD.ld3,a0:ADD.ld3,a0
  LSR d4,d1:MOVEQ#0,d4:MOVEQ#0,d5:MOVEQ#0,d6:ADD d1,d2
  MOVE.b(a0),d4:ADD d0,a0:MOVE.b(a0),d5:ADD d0,a0:MOVE.b(a0),d6
  MOVE d4,d7:ADD d5,d7:ADD d6,d7:EXT.l d7:DIVU #3,d7
  MOVEQ#0,d0:CMP #250,d7:BPL notg:CMP #255,d5:BPL dun ;SUB#5,d5:CMP d6,d5:BPL dun
  notg:MOVE d7,d6:SUB d4,d6:ADD #16,d6  ;change this if red spotting bad
  BPL notr:MOVEQ#3,d0:BTST#0,d2:BEQ dun
  CMP (a1),d4:BPL nhi:MOVEQ#1,d0:BRA dun:nhi
  CMP 2(a1),d4:BMI dun:MOVEQ#2,d0:BRA dun:notr:MOVEQ#2,d0
  MOVE 4(a1),d1:BTST#0,d2:BEQ nd
  MOVE 6(a1),d1:nd:CMP d1,d7:BPL dun:MOVEQ#1,d0:dun:AsmExit
End Function

thresh=120

.init
  InitBank 0,400*512,0
  InitBank 1,400*256,0
  InitShape 0,64,32,2:ShapesBitMap 0,0:Cls 3:MakeCookie 0 ;aga
  InitShape 1,32,32,2:ShapesBitMap 1,1:Cls 3:MakeCookie 1 ;ecs
  InitShape 2,64,32,2:ShapesBitMap 2,2:Cls 3:MakeCookie 2 ;ecs show
  InitShape 3,60,30,2
  Dim a.w(7):a(0)=160:a(1)=224:a(2)=64:a(3)=112:a(4)=19:a(5)=11:a(6)=3
  aga=1
  MaxLen pa$=192:MaxLen fi$=192
  PalRGB 0,0,8,10,12
  PalRGB 0,1,3,3,3
  PalRGB 0,2,12,12,12
  PalRGB 0,3,14,6,8
  t$="Imagine -> SkidMarks Frame Converter"
  t$+"                   (c)1994 Acid Software"
  Screen 0,0,0,640,256,2,$8000,t$,2,1:ShowPalette 0
  ;
  StringGadget 0,52,3,0,0,124,150:SetString 0,0,i$
  StringGadget 0,268,3,0,1,24,150:SetString 0,1,v$
  StringGadget 0,462,3,0,2,24,150:SetString 0,2,s$
  ;
  TextGadget 0,18,26,0,3, "SELECT FILE"
  TextGadget 0,18,42,0,4, " GENERATE  "
  TextGadget 0,18,58,0,5, "   QUIT    "

  TextGadget 0,128,26,0,6, "SHOW SHADOW"
  TextGadget 0,128,42,0,7, " SHOW AGA  "
  TextGadget 0,128,58,0,8," SHOW ECS  "

  For y=0 To 3
    TextGadget 0,546,22+y*13,0,10+y+y,"<"
    TextGadget 0,590,22+y*13,0,11+y+y,">"
    ;
    TextGadget 0,346,22+y*13,0,18+y+y,"<"
    TextGadget 0,390,22+y*13,0,19+y+y,">"
  Next
  ;
  Window 0,0,11,640,245,$1800,"",2,1,0
  ;
  GadgetBorder 12,3,610,12
  ;
  GadgetBorder 12,21,220,53
  GadgetBorder 256,21,170,53
  GadgetBorder 450,21,172,53
  ;
  GadgetBorder 12,81,610,142
  GadgetBorder 20,232,592,8
  ;
  For x=0 To 7
    For y=0 To 3
      GadgetBorder 40+x*70,84+y*35,60,30
      ShapeGadget 0,40+x*70,84+y*35,0,50+x+y*8,3
    Next
  Next
  ;
  GadgetBorder 12,229,610,14
  ;
  WLocate 012,5:Print "Path:"
  WLocate 224,5:Print "Name:"
  WLocate 438,5:Print "By:"
  Gosub carsettings
  ;
  #ysc=3:#xsc=4:#scal=5
  Dim qsin(31),qcos(31)   :; look up table for car motion
  For cnt.w=0 To 31:qcos(cnt)=Cos(cnt/16*Pi):qsin(cnt)=Sin(cnt/16*Pi):Next
  NEWTYPE .quad  :rx0.w:ry0:rx1:ry1:rx2:ry2:rx3:ry3:End NEWTYPE
  DEFTYPE .quad q
  ;
  WLocate 20,232:Print Centre$("SELECT CAR TO CONVERT",74)

.main
  Repeat
    ev.l=WaitEvent
    If ev=$40
      Select GadgetHit
        Case 0
          i$=StringText$(0,0):Gosub checkpath
        Case 1
          v$=StringText$(0,1):Gosub checkpath
        Case 2
          s$=StringText$(0,2):Gosub checkpath
        Case 4
          If f$<>""
            Gosub recordframes
            Gosub makeheader
          EndIf
        Case 5
          End
        Case 6
          If f$<>"" Then aga=1:For frame=fof To fof+31:Gosub convertshadow:Next
        Case 7
          If f$<>"" Then aga=1:For frame=1 To 32:Gosub convertframe:Next
        Case 8
          If f$<>"" Then aga=0:For frame=1 To 32:Gosub convertframe:Next
        ;
        Case 10:a(0)=QLimit(a(0)-4,0,256):Gosub carsettings
        Case 11:a(0)=QLimit(a(0)+4,0,256):Gosub carsettings
        Case 12:a(1)=QLimit(a(1)-4,0,256):Gosub carsettings
        Case 13:a(1)=QLimit(a(1)+4,0,256):Gosub carsettings
        Case 14:a(2)=QLimit(a(2)-4,0,256):Gosub carsettings
        Case 15:a(2)=QLimit(a(2)+4,0,256):Gosub carsettings
        Case 16:a(3)=QLimit(a(3)-4,0,256):Gosub carsettings
        Case 17:a(3)=QLimit(a(3)+4,0,256):Gosub carsettings
        ;
        Case 18:a(4)=QLimit(a(4)-1,0,32):Gosub carsettings
        Case 19:a(4)=QLimit(a(4)+1,0,32):Gosub carsettings
        Case 20:a(5)=QLimit(a(5)-1,0,32):Gosub carsettings
        Case 21:a(5)=QLimit(a(5)+1,0,32):Gosub carsettings
        Case 22:a(6)=QLimit(a(6)-1,-8,8):Gosub carsettings
        Case 23:a(6)=QLimit(a(6)+1,-8,8):Gosub carsettings
        Case 24:a(7)=QLimit(a(7)-1,-8,8):Gosub carsettings
        Case 25:a(7)=QLimit(a(7)+1,-8,8):Gosub carsettings
        ;
        Case 3
          f$=FileRequest$("SELECT PIC.0001 OF 800",pa$,fi$)
          p=0:pp=Instr(f$,"/")
          While pp<>0:r=p:p=pp:pp=Instr(f$,"/",p+1):Wend
          i$=Left$(f$,r)
          v$=Mid$(f$,r+1):p=Instr(v$,"."):v$=Left$(v$,p-1)
          pa$=Left$(f$,r)
          Gosub checkpath
        ;
        Default
          If GadgetHit>49
            InitShape 0,64,32,2:ShapesBitMap 0,0:Cls 3:MakeCookie 0
            frame=GadgetHit-49:Gosub convertframe
          EndIf
      End Select
    EndIf
  Forever

.carsettings:
  ;
  WLocate 262,25:NPrint "ShadowLen:"
  WLocate 262,38:NPrint "ShadowWid:"
  WLocate 262,51:NPrint "  XOffset:"
  WLocate 262,64:NPrint "  YOffset:"
  ;
  WLocate 472,25:NPrint "RedWhite:"
  WLocate 472,38:NPrint "RedBlack:"
  WLocate 472,51:NPrint "  GreyLo:"
  WLocate 472,64:NPrint "  GreyHi:"
  ;
  For y=0 To 3
    WLocate 364,25+y*13:Print Right$(Str$(1000+a(4+y)),3)
    WLocate 564,25+y*13:Print Right$(Str$(1000+a(y)),3)
  Next
  ;
  SetString 0,0,i$:SetString 0,1,v$:SetString 0,2,s$
  Redraw 0,0:Redraw 0,1:Redraw 0,2
  ;
  Return

.recordframes:
  aga=1
  For frame=1 To 800 Step 2
    Gosub convertframe
    fr.l=frame-1
    fr=(fr AND $ffe0)+((23-fr)AND31)
    SpriteMode 2:GetaSprite 0,0
    CopyMem_ Peek.l(Addr Sprite(0)),BankLoc(0)+fr/2*512,512
    SpriteMode 0:GetaSprite 0,1
    CopyMem_ Peek.l(Addr Sprite(0))    ,BankLoc(1)    +fr/2*256,128;256
    CopyMem_ Peek.l(Addr Sprite(0))+132 ,BankLoc(1)+128+fr/2*256,128;256
    xx.f=frame*596/800:WLine 18+xx,231,18+xx,240,3
  Next
  SaveBank 0,p$+v$+".aga"
  SaveBank 1,p$+v$+".ecs"
  Return

.makeheader
  frame=414:Gosub convertframe
  car.header\settings\speed=2,-4,-4,1,12,10,a(4),a(5)
  If WriteFile(0,p$+v$+".def")
    FileOutput 0:NPrint v$:NPrint s$
    WriteMem 0,&car\settings,SizeOf .handling
    WriteMem 0,Peek.l(Addr BitMap(0)+8),64*32/4
    CloseFile 0
  EndIf
  WindowOutput 0
  Return

.convertshadow:
  Use BitMap 0:Cls:USEPATH q
  xdim=a(4):ydim=a(5)
  ;
  d=(24-frame)AND31
  ;
  qc=qcos(d):qs=qsin(d)
  ri3=(-xdim*qc+ydim*qs) ASR #scal:rj3=(-xdim*qs-ydim*qc)ASR #scal
  ri2=(-xdim*qc-ydim*qs) ASR #scal:rj2=(-xdim*qs+ydim*qc)ASR #scal
  ri1=( xdim*qc-ydim*qs) ASR #scal:rj1=( xdim*qs+ydim*qc)ASR #scal
  ri0=( xdim*qc+ydim*qs) ASR #scal:rj0=( xdim*qs-ydim*qc)ASR #scal
  \ry3=ri3 ASL #ysc+rj3 ASL #ysc+25
  \ry2=ri2 ASL #ysc+rj2 ASL #ysc+25
  \ry1=ri1 ASL #ysc+rj1 ASL #ysc+25
  \ry0=ri0 ASL #ysc+rj0 ASL #ysc+25
  \rx3=QLimit(ri3 ASL #xsc-rj3 ASL #xsc+16,0,31)
  \rx2=QLimit(ri2 ASL #xsc-rj2 ASL #xsc+16,0,31)
  \rx1=QLimit(ri1 ASL #xsc-rj1 ASL #xsc+16,0,31)
  \rx0=QLimit(ri0 ASL #xsc-rj0 ASL #xsc+16,0,31)
  ;
  xoff=0:yoff=-3
  ;
  \rx0*2+xoff:\ry0+yoff
  \rx1*2+xoff:\ry1+yoff
  \rx2*2+xoff:\ry2+yoff
  \rx3*2+xoff:\ry3+yoff
  ;
  Polyf 4,&q\rx0,1
  ;
  Use BitMap 1:Cls:Use BitMap 2:Cls
  ;
  Goto doframe

.convertframe  ;frame=1..800 -> shape 0
  ;
  Use BitMap 0:Cls:Use BitMap 1:Cls:Use BitMap 2:Cls
  ;
doframe:
  ox=a(6):oy=a(7)
  ff$=f$+"pic."+Right$(Str$(10000+frame),4)
  If Exists(ff$)=0 Then Gosub fileerr:Return
  LoadChunky 0,ff$:m.l=Addr Chunky(0):Use BitMap 0
  For y=0 To 31:For x=0 To 63
    c=pickcolor{m,x+ox,y+oy,&a(0),0}
    If c<>0
      Use BitMap 0:Plot x,y,c
      If (x AND 1)=0
        c=pickcolor{m,x+ox,y+oy,&a(0),1}
        Use BitMap 1:Plot x LSR 1,y,c
        If aga=0 Then Use BitMap 2:Plot (x AND 254),y,c:Plot (x AND254)+1,y,c
      EndIf
    EndIf
  Next:Next
  dx=(frame-1) AND 7:dy=Int((frame-1) LSR 3) AND 3
  WBlit 2-aga-aga,38+dx*70,83+dy*35
  Return

.checkpath:
  gotit=0:f$=i$+v$+".pix/pic.0001"
  If Exists(f$)>0
    LoadChunky 0,f$
    If ChunkyLoc(0)
      fw=Peek.w(Addr Chunky(0)+16):fh=Peek.w(Addr Chunky(0)+18)
      f$=UnLeft$(f$,8)
      Gosub checkdef:Gosub carsettings
      c$=f$+"  ("+Str$(fw)+"x"+Str$(fh)+")"
      WLocate 20,232:Print Centre$(c$,74)
      gotit=1
    EndIf
  EndIf
  If gotit=0 Then f$="":Gosub fileerr
  Return

.checkdef:
  If ReadFile(0,p$+v$+".def")
    FileInput 0:n$=Edit$(80):s$=Edit$(80)
    ReadMem 0,&car\settings,SizeOf .handling
    CloseFile 0
    a(4)=car\settings\shadowx
    a(5)=car\settings\shadowy
  EndIf
  Return

.fileerr:
  BeepScreen 0
  WLocate 20,232:Print Centre$("COULD NOT LOAD 24BIT IFF FILE",74)
  Return

